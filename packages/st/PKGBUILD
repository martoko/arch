pkgname=st
pkgver=0.8.5
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
url='https://st.suckless.org'
source=("$pkgname::git+https://git.suckless.org/$pkgname#tag=$pkgver"
	'config.h')
sha256sums=('SKIP' 'SKIP')

prepare() {
  cd "$srcdir/$pkgname"
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$srcdir/$pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/$pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
