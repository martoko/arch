pkgname=dwm
pkgver=6.3.r5.6cd5a7e
pkgrel=1
pkgdesc='A dynamic window manager for X'
url='https://dwm.suckless.org'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libx11' 'libxinerama' 'libxft')
makedepends=('git')
source=("$pkgname::git+https://github.com/martoko/$pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$pkgname"
  make -B X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 config.h
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/$pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
